/*
 * Copyright (C) 2019 Michael Clarke
 *
 * This program is free software; you can redistribute it and/or
 * modify it under the terms of the GNU Lesser General Public
 * License as published by the Free Software Foundation; either
 * version 3 of the License, or (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
 * Lesser General Public License for more details.
 *
 * You should have received a copy of the GNU Lesser General Public License
 * along with this program; if not, write to the Free Software Foundation,
 * Inc., 51 Franklin Street, Fifth Floor, Boston, MA  02110-1301, USA.
 *
 */
plugins {
    id('java')
    id('jacoco')
    id('org.sonarqube') version('2.7')
    id('info.solidsoft.pitest') version('1.4.0')
    id('com.iadams.sonar-packaging') version('1.0-RC1')
    id('net.researchgate.release') version('2.6.0')
}

group 'com.github.mc1arke.sonarqube.plugin'

repositories {
    mavenCentral()
    ivy {
        url 'https://binaries.sonarsource.com/'
        layout 'pattern', {
            artifact '/Distribution/[module]/[module]-[revision].[ext]'
        }
    }
}

def sonarqubeVersion = '7.8'
def sonarqubeLibDir = "${projectDir}/sonarqube-lib"
def sonarLibraries = "${sonarqubeLibDir}/sonarqube-${sonarqubeVersion}/lib"

configurations {
    zip
}

compileJava {
    options.compilerArgs += '-proc:none'
}


dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
    compileOnly fileTree(dir: sonarLibraries, include: '**/*.jar')
    testCompile fileTree(dir: sonarLibraries, include: '**/*.jar')
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '2.24.0'
    zip "sonarqube:sonarqube:${sonarqubeVersion}@zip"
    compileOnly group: 'org.sonarsource.sonarqube', name: 'sonar-plugin-api', version: "7.7"
}


project.afterEvaluate {
    if (file("${sonarLibraries}").exists()) {
        return
    }
    println 'Extracting SonarQube libraries (this may take a while)...'
    configurations.zip.resolvedConfiguration.resolvedArtifacts.each { artifact ->
        copy {
            from zipTree(artifact.getFile())
            into "${sonarqubeLibDir}"
        }
    }
}

sonarPackaging {
    pluginKey = 'communityBranchPlugin'
    pluginClass = 'com.github.mc1arke.sonarqube.plugin.CommunityBranchPluginBootstrap'
    pluginName = 'Community Branch Plugin'
    pluginDescription = 'Enables branch and pull request analysis in SonarQube Community Edition, without having to upgrade to Developer Edition'
    pluginLicense = 'GNU LGPL 3'
    pluginUrl = 'https://github.com/mc1arke/sonarqube-community-branch-plugin'
    pluginIssueTrackerUrl = 'https://github.com/mc1arke/sonarqube-community-branch-plugin/issues'
    skipDependenciesPackaging = true
    useChildFirstClassLoader = false
    organization['name'] = 'Michael Clarke'
    organization['url'] = ''
}

release {
    git {
        requireBranch = '(master|release-\\d+\\.\\d+|\\d+\\.\\d+)'
    }
}